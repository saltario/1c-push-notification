
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ЗаполнитьРасписаниеНаСервере();
	ОповеститьОбИзменении(ПараметрКоманды);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРасписаниеНаСервере()
	
	// Получить начало этой недели.
	НачалоЭтойНедели = НачалоНедели(ТекущаяДатаСеанса());
	
	// Перебрать все дни этой недели
	Для КоличествоДобавляемыхДней = 0 По 4 Цикл
		ДатаСтарого  = НачалоЭтойНедели + 60 * 60 * 24 * КоличествоДобавляемыхДней;
		НомерСтарого = ОбщегоНазначенияСервер.ПолучитьНомер(ДатаСтарого);
		
		// Проверить, что в базе есть старый документ-оригинал.
		СтарыйДокумент = Документы.УчебныйДень.НайтиПоНомеру(НомерСтарого);	
		
		// В базе нет документа-оригинала - перейти к следующему дню недели.
		Если СтарыйДокумент.Пустая() Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "На " + Формат(ДатаСтарого, "ДФ='д ММММ гггг, дддд'") + " нет учебного дня.";
			Сообщение.Сообщить();
			
			Продолжить;
			
		КонецЕсли; 
		
		// Вычислить дату и номер для нового документа.
		ДатаНового  = ДатаСтарого + 60 * 60 * 24 * 7;   // к дате старого прибавить неделю
		НомерНового = ОбщегоНазначенияСервер.ПолучитьНомер(ДатаНового);
		
		// Проверить, что в базе ещё нет нового документа на эту дату.
		Если Документы.УчебныйДень.НайтиПоНомеру(НомерНового).Пустая() Тогда
			
			// Создать и записать новый документ.
			НовыйДокумент = Документы.УчебныйДень.СоздатьДокумент();
			НовыйДокумент.Дата = ДатаНового;
			Для Каждого СтрокаСтарого Из СтарыйДокумент.Уроки Цикл
				НоваяСтрока = НовыйДокумент.Уроки.Добавить();
				НоваяСтрока.Предмет = СтрокаСтарого.Предмет;
				
			КонецЦикла;
			
			НовыйДокумент.Записать();
			
		Иначе	
			
			// Уже есть новый документ на эту дату - перейти к следующему дню недели.
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Уже есть учебный день " + Формат(ДатаНового, "ДФ='д ММММ гггг, дддд'");
			Сообщение.Сообщить();
			
		КонецЕсли;				
		
	КонецЦикла;
	
КонецПроцедуры